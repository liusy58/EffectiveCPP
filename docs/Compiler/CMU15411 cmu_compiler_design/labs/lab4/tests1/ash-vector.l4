#test return 15

struct List {
  head : int;
  tail : List*;
};

// test vector operations

int main() {
  var test, N, sumN, sumsqN : int;
  var a,b,c,d : List*;
  
  test = 0;
  N = 1000; // huge
  
  sumN = (N + 1) * N / 2;
  sumsqN = (2*N + 1) * (N + 1) * N / 6;
  
  a = upto(N);
  b = scale(a, 3);
  c = dot(a, a);
  d = add(b, c); 
 
  test |= ((sum(a) == sumN) ? 1 : 0);
  test |= ((sum(b) == 3*sumN) ? 1 : 0) << 1;
  test |= ((sum(c) == sumsqN) ? 1 : 0) << 2;
  test |= ((sum(d) == sumsqN+3*sumN) ? 1 : 0) << 3;
  
  return test;
}

/* vector operations */

List* scale(a : List*, scalar : int) {
  if (isEmpty(a))
    return nil();
  else	
    return cons(a->head * scalar, scale(a->tail, scalar));
}

List* add(a : List*, b: List*) {
  if (isEmpty(a) || isEmpty(b))
    return nil();
  else	
    return cons(a->head + b-> head, add(a->tail, b->tail));
}

List* dot(a : List*, b: List*) {
  if (isEmpty(a) || isEmpty(b))
    return nil();
  else	
    return cons(a->head * b-> head, dot(a->tail, b->tail));
}

/*  list analysis */

int length(list : List*) {
  return foldLeft_int(0, list, 1);
}

bool onlyZeroes(list : List*) {
  return foldLeft_int(1, list, 2) != 0;
}

int sum(list : List*) {
  return foldLeft_int(0, list, 3);
}

bool isSorted(list : List*) {
  var min : int;
  min = -2147483648;
  while (!isEmpty(list)) {
    if (list->head < min) return false;
    else min = list->head;
    list = list->tail;
  }
  return true; 
}

bool equal(as : List*, bs : List*) {
  return !((isEmpty(as) && !isEmpty(bs) || !isEmpty(as) && isEmpty(bs))) && (isEmpty(as) || (as->head == bs->head && equal(as->tail, bs->tail)));
} 

int foldLeft_int(z : int, list : List*, funselect : int) {
  while (!isEmpty(list)) {
    // length
    if (funselect == 1)
	  z += 1;
	// onlyZeroes
	else if (funselect == 2)
	  z = (z != 0) && (list->head == 0) ? 1 : 0;
	//  sum
	else if (funselect == 3)
	  z += list->head;
	// ...
	
	list = list->tail;
  }
  return z;
}

/* list creation */

// list [start, ..., end)
List* range(start : int, end : int) {
  if (start < end)
    return cons(start, range(start + 1, end));
  else
    return nil();
}

// list [1, ..., end]
List* upto(end : int) {
  return range(1, end + 1);
}

/* basic list */

List* nil() {
  return NULL;
}

List* cons(i : int, list : List*) {
  var newList : List*;
  newList = new(List);
  newList->head = i;
  newList->tail = list;
  return newList;
}

bool isEmpty(list : List*) {
  return list == NULL;
}
